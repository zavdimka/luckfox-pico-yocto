# Luckfox Pico RV1106 machine definition
require conf/machine/include/arm/armv7a/tune-cortexa7.inc

SOC_FAMILY = "rockchip"
MACHINEOVERRIDES =. "rockchip:rv1106:"

SERIAL_CONSOLE = "ttyFIQ0,115200n8"
KERNEL_IMAGETYPE = "Image"

# Device tree source file (machine-specific)
KERNEL_DTS_FILE = "rv1106g-av.dts"

UBOOT_MACHINE = "luckfox_rv1106_uboot_defconfig"
UBOOT_SUFFIX = "img"
UBOOT_ENTRYPOINT = "0x00200000"

# SDK-style boot medium configuration (matches SDK BoardConfig files)
# Options: sd_card, emmc, spi_nand, spi_nor
RK_BOOT_MEDIUM = "emmc"

# A/B Update Configuration
# Enable dual boot A/B partitions for OTA updates with automatic rollback
RK_ENABLE_AB_UPDATE ?= "1"

# Partition layout (SDK-compatible format)
# Format: size@offset(name) where offset is optional (auto-calculated if omitted)
# Size units: K (KB), M (MB), G (GB), - (fill remaining space)
# Example from SDK: "32K(env),256K@32K(idblock),256K(uboot),32M(boot),-(rootfs)"
# Default: Single boot partition layout with userdata
# RK_PARTITION_LAYOUT = "32K(env),256K@32K(idblock),256K(uboot),16M(boot),256M(rootfs),-(userdata:ext4)"
# A/B layout: boot_a/boot_b (16MB each), rootfs_a/rootfs_b (256MB each), userdata (remaining)
RK_PARTITION_LAYOUT = "32K(env),256K@32K(idblock),256K(uboot),16M(boot_a),16M(boot_b),256M(rootfs_a),256M(rootfs_b),-(userdata:ext4)"
#partition numbers starts from 1 for mmcblk0p1, mmcblk0p2, ...
# Single boot: rootfs = mmcblk0p5, userdata = mmcblk0p6
# Note: root= will be set dynamically by U-Boot based on active_slot
# RK_BOOT_ROOT_ARGS = "root=/dev/mmcblk0p5 rootfstype=ext4"
#for A/B update use the following instead
RK_BOOT_ROOT_ARGS = "root=/dev/mmcblk0p6 rootfstype=ext4"

# Fastboot mode (uses _TB variants of INI files)
RK_ENABLE_FASTBOOT ?= "n"

# U-Boot defconfig settings (matching SDK build.sh logic)
RK_UBOOT_DEFCONFIG = "luckfox_rv1106_uboot_defconfig"

PREFERRED_PROVIDER_virtual/kernel = "linux-luckfox"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-luckfox"

# Kernel version is 5.10.160 - set OLDEST_KERNEL to match
OLDEST_KERNEL = "5.10"

MACHINE_FEATURES += "wifi usbgadget ext4"

# Keep rootfs/ext4 layout aligned with the legacy SDK
EXTRA_IMAGEDEPENDS += "u-boot-luckfox"
